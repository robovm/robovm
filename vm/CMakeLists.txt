# The Mac OS X SDK version required when building the VM libs
set(MACOSX_SDK_VERSION 10.9)
# The minimum Mac OS X version needed by apps built by RoboVM
set(MACOSX_MIN_VERSION 10.6)
# The iOS SDK version required when building the VM libs
set(IOS_SDK_VERSION 8.1)
# The minimum iOS version needed by apps built by RoboVM
set(IOS_MIN_VERSION 6.0)

cmake_minimum_required(VERSION 2.8.8)
project(build)
include(CheckCCompilerFlag)
include(ExternalProject)

enable_testing()

if(NOT CMAKE_BUILD_TYPE)
  set(CMAKE_BUILD_TYPE "debug")
endif()


if(NOT OS)
  message(WARNING "OS not set, trying to determine automatically")
  IF(${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
    SET(OS "macosx")
    SET(ARCH "x86_64")
  ENDIF()
  IF(${CMAKE_SYSTEM_NAME} MATCHES "Linux")
    SET(OS "linux")
    SET(ARCH "x86_64")
  ENDIF()
endif()
if(NOT OS MATCHES "^(linux|macosx|ios)$")
  message(FATAL_ERROR "Unsupported OS: ${OS}")
endif()
if(NOT ARCH)
  message(FATAL_ERROR "ARCH not set")
endif()
if(NOT ARCH MATCHES "^(x86_64|x86|thumbv7|arm64)$")
  message(FATAL_ERROR "Unsupported ARCH: ${ARCH}")
endif()

if(OS STREQUAL "linux")
  set(LINUX YES)
  set(OS_FAMILY linux)
elseif(OS STREQUAL "macosx")
  set(MACOSX YES)
  set(DARWIN YES)
  set(OS_FAMILY darwin)
elseif(OS STREQUAL "ios")
  set(IOS YES)
  set(DARWIN YES)
  set(OS_FAMILY darwin)
endif()

set(64_BIT NO)
if (ARCH STREQUAL "x86_64")
  set(X86_64 YES)
  set(64_BIT YES)
elseif (ARCH STREQUAL "x86")
  set(X86 YES)
elseif (ARCH STREQUAL "thumbv7")
  set(ARM YES)
  set(THUMB YES)
  set(THUMBV7 YES)
elseif (ARCH STREQUAL "arm64")
  set(ARM64 YES)
  set(64_BIT YES)
endif()

if(DARWIN)
  set(CARCH ${ARCH})
  if (ARCH STREQUAL "x86_64")
    set(CARCH x86_64)
  elseif (ARCH STREQUAL "x86")
    set(CARCH i386)
  elseif (ARCH STREQUAL "thumbv7")
    set(CARCH armv7)
  elseif (ARCH STREQUAL "arm64")
    set(CARCH arm64)
  endif()
endif()

if(DARWIN)
  if(NOT SYSROOT)
    exec_program(xcode-select ARGS --print-path OUTPUT_VARIABLE XCODE_PATH)
    if(MACOSX)
      set(SYSROOT "${XCODE_PATH}/Platforms/MacOSX.platform/Developer/SDKs/MacOSX${MACOSX_SDK_VERSION}.sdk")
      if(NOT IS_DIRECTORY ${SYSROOT})
        message(FATAL_ERROR "Failed to locate MacOSX SDK ${MACOSX_SDK_VERSION}")
      endif()
    elseif(IOS)
      if(ARCH MATCHES "^(x86|x86_64)$")
        set(SYSROOT "${XCODE_PATH}/Platforms/iPhoneSimulator.platform/Developer/SDKs/iPhoneSimulator${IOS_SDK_VERSION}.sdk")
        if(NOT IS_DIRECTORY ${SYSROOT})
          message(FATAL_ERROR "Failed to locate iPhoneSimulator SDK ${IOS_SDK_VERSION}")
        endif()
      else()
        set(SYSROOT "${XCODE_PATH}/Platforms/iPhoneOS.platform/Developer/SDKs/iPhoneOS${IOS_SDK_VERSION}.sdk")
        if(NOT IS_DIRECTORY ${SYSROOT})
          message(FATAL_ERROR "Failed to locate iPhoneOS SDK ${IOS_SDK_VERSION}")
        endif()
      endif()
    endif()
  endif()
endif()

# Set up global C and C++ compiler flags
set(C_FLAGS "${C_FLAGS} -std=gnu99")
set(CXX_FLAGS "${CXX_FLAGS} -std=gnu++11")
set(C_CXX_FLAGS "${C_CXX_FLAGS} -Wall -fvisibility=hidden -fdata-sections -ffunction-sections")
if(CARCH)
  set(C_CXX_FLAGS "${C_CXX_FLAGS} -arch ${CARCH}")
elseif(64_BIT)
  set(C_CXX_FLAGS "${C_CXX_FLAGS} -m64")
else()
  set(C_CXX_FLAGS "${C_CXX_FLAGS} -m32")
endif()
if (ARCH STREQUAL "thumbv6" OR ARCH STREQUAL "thumbv7")
  set(C_CXX_FLAGS "${C_CXX_FLAGS} -mthumb")
endif()
if(SYSROOT)
  set(C_CXX_FLAGS "${C_CXX_FLAGS} -isysroot ${SYSROOT}")
  set(CMAKE_OSX_SYSROOT "${SYSROOT}")
endif()
if(MACOSX)
  set(C_CXX_FLAGS "${C_CXX_FLAGS} -mmacosx-version-min=${MACOSX_MIN_VERSION}")
elseif(IOS)
  set(C_CXX_FLAGS "${C_CXX_FLAGS} -miphoneos-version-min=${IOS_MIN_VERSION}")
endif()

# Linker flags
if(CARCH)
  set(LD_FLAGS "${LD_FLAGS} -arch ${CARCH}")
elseif(64_BIT)
  set(LD_FLAGS "${LD_FLAGS} -m64")
else()
  set(LD_FLAGS "${LD_FLAGS} -m32")
endif()
if(SYSROOT)
  set(LD_FLAGS "${LD_FLAGS} -isysroot ${SYSROOT}")
endif()
if(MACOSX)
  set(LD_FLAGS "${LD_FLAGS} -mmacosx-version-min=${MACOSX_MIN_VERSION}")
endif()
if(IOS)
  set(LD_FLAGS "${LD_FLAGS} -miphoneos-version-min=${IOS_MIN_VERSION}")
endif()

set(CMAKE_C_FLAGS_RELEASE "-Os -DNDEBUG")
set(CMAKE_CXX_FLAGS_RELEASE "-Os -DNDEBUG")

set(CMAKE_C_FLAGS_DEBUG "${CMAKE_C_FLAGS_DEBUG} ${C_CXX_FLAGS} ${C_FLAGS}")
set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} ${C_CXX_FLAGS} ${CXX_FLAGS}")
set(CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE} ${C_CXX_FLAGS} ${C_FLAGS}")
set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} ${C_CXX_FLAGS} ${CXX_FLAGS}")
set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${LD_FLAGS}")
set(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} ${LD_FLAGS}")
set(CMAKE_MODULE_LINKER_FLAGS "${CMAKE_MODULE_LINKER_FLAGS} ${LD_FLAGS}")

# Assembler flags
if(CARCH)
  set(ASM_FLAGS "${ASM_FLAGS} -c -arch ${CARCH}")
elseif(64_BIT)
  set(ASM_FLAGS "${ASM_FLAGS} -c -m64")
else()
  set(ASM_FLAGS "${ASM_FLAGS} -c -m32")
endif()
if (CMAKE_BUILD_TYPE STREQUAL "debug")
  set(ASM_FLAGS "${ASM_FLAGS} -g")
endif()
set(CMAKE_ASM_COMPILE_OBJECT "${CMAKE_C_COMPILER} ${ASM_FLAGS} -o <OBJECT> <SOURCE>")
enable_language(ASM)

if(LINUX)
  add_definitions(-DLINUX)
elseif(DARWIN)
  if(MACOSX)
    add_definitions(-DMACOSX)
  else()
    add_definitions(-DIOS)
  endif()
  add_definitions(-DDARWIN)
endif()

if (ARCH STREQUAL "x86_64")
  add_definitions(-DRVM_X86_64)
elseif (ARCH STREQUAL "x86")
  add_definitions(-DRVM_X86)
elseif (ARCH STREQUAL "thumbv7")
  add_definitions(-DRVM_THUMBV7)
elseif (ARCH STREQUAL "arm64")
  add_definitions(-DRVM_ARM64)
endif()

set(LIB_SUFFIX ".a")
if (CMAKE_BUILD_TYPE STREQUAL "debug")
  set(LIB_SUFFIX "-dbg.a")
endif()

set(INSTALL_DIR ${CMAKE_SOURCE_DIR}/target/binaries/${OS}/${ARCH})

message(STATUS "ARCH: ${ARCH}")
message(STATUS "OS: ${OS}")
message(STATUS "SYSROOT: ${SYSROOT}")
message(STATUS "CMAKE_BUILD_TYPE: ${CMAKE_BUILD_TYPE}")
message(STATUS "CMAKE_C_COMPILER: ${CMAKE_C_COMPILER}")
message(STATUS "CMAKE_CXX_COMPILER: ${CMAKE_CXX_COMPILER}")
message(STATUS "CMAKE_C_FLAGS: ${CMAKE_C_FLAGS}")
message(STATUS "CMAKE_CXX_FLAGS: ${CMAKE_CXX_FLAGS}")
message(STATUS "CMAKE_C_FLAGS_DEBUG: ${CMAKE_C_FLAGS_DEBUG}")
message(STATUS "CMAKE_CXX_FLAGS_DEBUG: ${CMAKE_CXX_FLAGS_DEBUG}")
message(STATUS "CMAKE_C_FLAGS_RELEASE: ${CMAKE_C_FLAGS_RELEASE}")
message(STATUS "CMAKE_CXX_FLAGS_RELEASE: ${CMAKE_CXX_FLAGS_RELEASE}")
message(STATUS "CMAKE_EXE_LINKER_FLAGS: ${CMAKE_EXE_LINKER_FLAGS}")
message(STATUS "CMAKE_SHARED_LINKER_FLAGS: ${CMAKE_SHARED_LINKER_FLAGS}")
message(STATUS "CMAKE_MODULE_LINKER_FLAGS: ${CMAKE_MODULE_LINKER_FLAGS}")

# Ugly hardcoding. This is the offset in bytes of the gcDescriptor member in the Class struct from core/include/robovm/types.h
# Should be the value of sizeof(void*)*3 for the target platform.
if(64_BIT)
  set(EXTGC_MARK_DESCR_OFFSET 24)
else()
  set(EXTGC_MARK_DESCR_OFFSET 12)
endif()

set(EXT_C_FLAGS "${C_CXX_FLAGS}")
set(EXT_LD_FLAGS "${CMAKE_EXE_LINKER_FLAGS}")
set(EXTGC_C_FLAGS "${EXT_C_FLAGS} -DGC_DISABLE_INCREMENTAL -DGC_FORCE_UNMAP_ON_GCOLLECT -DMARK_DESCR_OFFSET=${EXTGC_MARK_DESCR_OFFSET}")
set(EXTGC_LD_FLAGS "${EXT_LD_FLAGS}")
if(DARWIN)
  set(EXTGC_C_FLAGS "${EXTGC_C_FLAGS} -DNO_DYLD_BIND_FULLY_IMAGE")
endif()
set(EXTGC_PARAMS  "--enable-shared=no --enable-munmap=1")
if (CMAKE_BUILD_TYPE STREQUAL "debug")
  set(EXTGC_C_FLAGS "${EXTGC_C_FLAGS} -g")
  set(EXTGC_PARAMS "${EXTGC_PARAMS} --enable-gc-debug=yes")
endif()
if (IOS)
  if(64_BIT)
    set(EXT_HOST --host=aarch64-apple-darwin7)
  else()
    set(EXT_HOST --host=arm-apple-darwin7)
  endif()
endif()
ExternalProject_Add(libatomic_ops 
  GIT_REPOSITORY git://github.com/ivmai/libatomic_ops.git
  GIT_TAG 4e22546e8a161b6bab93e43c115a22151daf45c5
  UPDATE_COMMAND ""
  CONFIGURE_COMMAND "" 
  BUILD_COMMAND ""
  INSTALL_COMMAND ""
  BUILD_IN_SOURCE 1
)
ExternalProject_Add(extgc 
  DEPENDS libatomic_ops
  GIT_REPOSITORY git://github.com/robovm/bdwgc.git
  GIT_TAG 330797462474976093d631058f784c843040e294
  UPDATE_COMMAND ""
  PATCH_COMMAND bash -c "rm -rf libatomic_ops && ln -s ../../../libatomic_ops-prefix/src/libatomic_ops libatomic_ops"
  CONFIGURE_COMMAND bash -c "CC=${CMAKE_C_COMPILER} CFLAGS='${EXTGC_C_FLAGS}' LDFLAGS='${EXTGC_LD_FLAGS}' ./configure ${EXT_HOST} ${EXTGC_PARAMS} --prefix=${CMAKE_BINARY_DIR}/gc"
  BUILD_IN_SOURCE 1
)
install(FILES ${CMAKE_BINARY_DIR}/gc/lib/libgc.a DESTINATION ${INSTALL_DIR} RENAME libgc${LIB_SUFFIX})

add_subdirectory(core/src)
add_subdirectory(debug/src)
add_subdirectory(bc/src)
add_subdirectory(rt)
add_subdirectory(bclib/src)
